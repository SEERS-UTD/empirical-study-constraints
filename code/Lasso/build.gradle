apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

version = '0.1'

// Unpacks source code and textual artifacts of target systems used in experiments
task unpackData(type: Copy) {
    def dataDir = file('../../data')

    // Must use a simple into call as base directory first
    into dataDir

    dataDir.traverse(
            type: groovy.io.FileType.FILES,
            maxDepth: 1,
            nameFilter: ~/^(sources|docs-txt|binaries)\.zip$/
    ) { zip ->
        into(zip.parentFile.name) {
            from zipTree(zip)
        }
    }
}

// Every time Lasso is compiled, the experimental data will be extracted if it hasn't been already
compileJava {
    dependsOn unpackData
}

repositories {
    mavenCentral()
}

dependencies {
    // WALA 1.5.3
    implementation project(':WALA:com.ibm.wala.core')
    implementation project(':WALA:com.ibm.wala.shrike')
    implementation project(':WALA:com.ibm.wala.util')

    implementation project(':seers-base')

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

    // https://mvnrepository.com/artifact/net.sourceforge.argparse4j/argparse4j
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'

    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.15.17'

    testImplementation("org.assertj:assertj-core:3.11.1")
    testImplementation group: 'junit', name: 'junit', version: '4.+'

    // https://mvnrepository.com/artifact/pl.pragmatists/JUnitParams
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

}

if (project.hasProperty('mainClass')) {
    // Use -PmainClass=my.class to override
    mainClassName = project.getProperty('mainClass')
} else {
    // use a default
    mainClassName = "edu.utdallas.seers.lasso.experiment.PatternDetectionEvaluator"
}

// Use --args to pass arguments to the run task
application {
    // Use -PjvmArgs="-Xsomething" to override
    if (project.hasProperty('jvmArgs')) {
        applicationDefaultJvmArgs = Arrays.asList project.getProperty('jvmArgs').split()
    } else {
        applicationDefaultJvmArgs = ['-Xms1g', '-Xmx10g']
    }
}


